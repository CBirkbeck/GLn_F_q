import Mathlib.FieldTheory.Finite.GaloisField
import Mathlib.LinearAlgebra.Matrix.GeneralLinearGroup
import Mathlib.Tactic.Have

open Matrix BigOperators

variable (p n m : â„•) [Fact (Nat.Prime p)]

local notation "ğ”½" => (GaloisField p m)

noncomputable instance {k : â„•} :
    Fintype ({ s : Fin k â†’ (Fin n â†’ ğ”½) // LinearIndependent ğ”½ s}) :=
  Fintype.ofFinite _

noncomputable instance {s : { s : Fin k â†’ Fin n â†’ ğ”½ // LinearIndependent ğ”½ s }} :
    Fintype (Submodule.span ğ”½ (Set.range (s : Fin k â†’ Fin n â†’ ğ”½)) : Set (Fin n â†’ ğ”½)) :=
  Fintype.ofFinite _

noncomputable instance {s : { s : Fin k â†’ Fin n â†’ ğ”½ // LinearIndependent ğ”½ s }} :
    Fintype ((Submodule.span ğ”½ (Set.range (s : Fin k â†’ Fin n â†’ ğ”½)))á¶œ : Set (Fin n â†’ ğ”½)) :=
  Fintype.ofFinite _

lemma complement_card (hm : m â‰  0) (s : { s : Fin k â†’ Fin n â†’ ğ”½ // LinearIndependent ğ”½ s }):
    Fintype.card ((Submodule.span ğ”½ (Set.range (s : Fin k â†’ Fin n â†’ ğ”½)))á¶œ : Set (Fin n â†’ ğ”½)) =
      (p ^ m) ^ n - (p ^ m) ^ k := by
  rw [Fintype.card_compl_set, Fintype.card_fun, GaloisField.card _ _ hm, Fintype.card_fin]
  simp only [SetLike.coe_sort_coe]
  rw [card_eq_pow_finrank (K := ğ”½), finrank_span_eq_card s.property, GaloisField.card _ _ hm,
    Fintype.card_fin]

def inductiveStepEquiv (k : â„•) :
    { s : Fin (k + 1) â†’ Fin n â†’ ğ”½ // LinearIndependent ğ”½ s } â‰ƒ
      Î£ s : { s : Fin k â†’ Fin n â†’ ğ”½ // LinearIndependent ğ”½ s },
        ((Submodule.span ğ”½ (Set.range (s : Fin k â†’ Fin n â†’ ğ”½)))á¶œ : Set (Fin n â†’ ğ”½)) where
  toFun s := by
    have := linearIndependent_fin_succ.mp s.property
    use âŸ¨Fin.tail s.val, this.leftâŸ©
    exact âŸ¨s.val 0, this.rightâŸ©
  invFun s := by
    use Fin.cons s.2.val s.1.val
    exact linearIndependent_fin_cons.mpr âŸ¨s.1.property, s.2.propertyâŸ©
  left_inv _ := by simp only [Fin.cons_self_tail, Subtype.coe_eta]
  right_inv := fun âŸ¨_, _âŸ© => by simp only [Fin.cons_zero, Subtype.coe_eta, Sigma.mk.inj_iff,
    Fin.tail_cons, heq_eq_eq, and_self]

lemma inductive_step_card (hm : m â‰  0) (k : â„•) :
    Fintype.card { s : Fin (k + 1) â†’ Fin n â†’ ğ”½ // LinearIndependent ğ”½ s } =
      Fintype.card { s : Fin k â†’ Fin n â†’ ğ”½ // LinearIndependent ğ”½ s } *
      ((p ^ m) ^ n - (p ^ m) ^k) := by
  rw [Fintype.card_congr (inductiveStepEquiv p n m k), Fintype.card_sigma]
  simp only [complement_card p n m hm _, Finset.sum_const]
  rfl

lemma step2 (hm : m â‰  0) {k : â„•} (hk : k â‰¤ n) :
    Fintype.card { s : Fin k â†’ (Fin n â†’ ğ”½) // LinearIndependent ğ”½ s } =
      âˆ i : Fin k, ((p ^ m) ^ n - (p ^ m) ^ i.val) := by
  induction' k with k ih
  Â· simp only [Nat.zero_eq, LinearIndependent, Finsupp.total_fin_zero, LinearMap.ker_zero,
    Fintype.card_ofSubsingleton, Finset.univ_eq_empty, Finset.prod_empty]
  Â· simp only [inductive_step_card p n m hm k, ih (Nat.le_of_succ_le hk), mul_comm,
    Fin.prod_univ_succAbove _ k, Fin.cast_nat_eq_last, Fin.val_last, Fin.succAbove_last,
    Fin.coe_castSucc]

lemma card_linearIndependent (hm : m â‰  0):
    Fintype.card { s : Fin n â†’ (Fin n â†’ ğ”½) // LinearIndependent ğ”½ s } =
      âˆ i : Fin n, (p ^ (m * n) - p ^ (m * i)) := by
  rw [step2 _ _ _ hm]
  apply Finset.prod_congr
  Â· rfl
  Â· simp only [Finset.mem_univ, forall_true_left]
    intro x
    ring_nf
  Â· simp only [le_refl]

variable (R : Type) [CommRing R]

def GLequiv : GL (Fin n) R â‰ƒ* (((Fin n) â†’ R) â‰ƒâ‚—[R] ((Fin n) â†’ R)) :=
  MulEquiv.trans
    Matrix.GeneralLinearGroup.toLinear
    (LinearMap.GeneralLinearGroup.generalLinearEquiv R _)

def Basis_equiv : Basis (Fin n) R ((Fin n) â†’ R) â‰ƒ ((Fin n) â†’ R) â‰ƒâ‚—[R] ((Fin n) â†’â‚€ R) where
  toFun := Basis.repr
  invFun := Basis.ofRepr
  left_inv := fun b â†¦ by cases b; rfl
  right_inv := fun b â†¦ rfl

noncomputable def equiv_finsupp : ((Fin n) â†’ R) â‰ƒâ‚—[R] ((Fin n) â†’â‚€ R) := by
  exact LinearEquiv.symm (Finsupp.linearEquivFunOnFinite R R (Fin n))

noncomputable def equiv_linmap: (((Fin n) â†’ R) â‰ƒâ‚—[R] ((Fin n) â†’ R)) â‰ƒ
    (((Fin n) â†’ R) â‰ƒâ‚—[R] ((Fin n) â†’â‚€ R)) where
  toFun := fun f â†¦ (f.trans (equiv_finsupp n R)  : (((Fin n) â†’ R) â‰ƒâ‚—[R] ((Fin n) â†’â‚€ R)))
  invFun := fun f â†¦ (f.trans (equiv_finsupp n R).symm  : (((Fin n) â†’ R) â‰ƒâ‚—[R] ((Fin n) â†’ R)))
  left_inv := congrFun rfl
  right_inv := by
    apply congrFun
    ext
    rfl

noncomputable def equiv_GL_basis : GL (Fin n) R â‰ƒ Basis (Fin n) R ((Fin n) â†’ R) := by
  apply Equiv.trans (GLequiv _ _).toEquiv
  apply Equiv.trans (equiv_linmap _ _)
  exact (Basis_equiv n R).symm

noncomputable def equiv_basis_linearindependent (hn : 0 < n) : Basis (Fin n) ğ”½ ((Fin n) â†’ ğ”½) â‰ƒ
    { s : Fin n â†’ (Fin n â†’ ğ”½) // LinearIndependent ğ”½ s } where
  toFun := fun b â†¦ âŸ¨b,Basis.linearIndependent _âŸ©
  invFun := by
    intro âŸ¨s,hsâŸ©
    have : Nonempty (Fin n) := Fin.pos_iff_nonempty.1 hn
    apply basisOfLinearIndependentOfCardEqFinrank hs
    simp only [Fintype.card_fin, FiniteDimensional.finrank_fintype_fun_eq_card]
  left_inv := by
    intro b
    apply DFunLike.ext'
    simp only [coe_basisOfLinearIndependentOfCardEqFinrank]
  right_inv := by
    intro âŸ¨s,hsâŸ©
    simp only [coe_basisOfLinearIndependentOfCardEqFinrank]

noncomputable instance fintype : Fintype (GL (Fin n) (GaloisField p m)) := by
    exact Fintype.ofFinite (GL (Fin n) (GaloisField p m))

noncomputable instance : Fintype (Basis (Fin n) ğ”½ ((Fin n) â†’ ğ”½)) :=
    Fintype.ofEquiv _ (equiv_GL_basis n ğ”½)

lemma card_GL (hm : m â‰  0): Fintype.card (GL (Fin n) (GaloisField p m)) =
        âˆ i : (Fin n), (p ^ (m * n) - p ^ (m * i)) := by
    by_cases hn : n = 0
    Â· rw [hn]
      simp only [Fintype.card_unique, Finset.univ_eq_empty, mul_zero, pow_zero,
      Finset.prod_empty]
    Â· rw [Fintype.card_congr (equiv_GL_basis n ğ”½), â† (card_linearIndependent p n m hm),
      Fintype.card_congr (equiv_basis_linearindependent p n m (Nat.pos_of_ne_zero hn))]
